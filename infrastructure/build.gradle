plugins {
    id 'java-library'
    id 'java-conventions'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.flywaydb.flyway' version '10.13.0'
    id 'jacoco-report-aggregation'
    id 'com.github.nbaztec.coveralls-jacoco' version '1.2.20'
}

group = 'com.kaua.order.infrastructure'

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation 'mysql:mysql-connector-java:8.0.33'

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.kafka:spring-kafka'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.2.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    implementation 'com.fasterxml.jackson.module:jackson-module-afterburner'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'com.h2database:h2'

    testImplementation(project(path: ':domain', configuration: 'testClasses'))
}

flyway {
    url = 'jdbc:mysql://' + System.getenv('DATABASE_MYSQL_URL') + '/ecommerce' ?: 'jdbc:mysql://localhost:3306/ecommerce'
    user = System.getenv('DATABASE_MYSQL_USERNAME') ?: 'root'
    password = System.getenv('DATABASE_MYSQL_PASSWORD') ?: '123456'
}

testCodeCoverageReport {
    reports {
        csv.required = true
        csv.outputLocation = file("$rootDir/build/reports/jacoco/test/jacocoTestReport.csv")
        html.required = true
        html.outputLocation = file("$rootDir/build/reports/jacoco/test/jacocoTestReport.html")
        xml.required = true
        xml.outputLocation = file("$rootDir/build/reports/jacoco/test/jacocoTestReport.xml")
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            'com/kaua/order/infrastructure/configurations/*',
                            'com/kaua/order/infrastructure/Main.class'
                    ])
                })
        )
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            'com/kaua/order/infrastructure/configurations/*',
                            'com/kaua/order/infrastructure/Main.class'
                    ])
                })
        )
    }
}

coverallsJacoco {
    reportPath = file("$rootDir/build/reports/jacoco/test/jacocoTestReport.xml")
}

tasks.named("jacocoTestReport") {
    dependsOn(testCodeCoverageReport)
}